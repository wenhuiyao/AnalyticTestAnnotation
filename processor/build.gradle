apply plugin: 'java'

// This is important even if Android Studio claims it isn't
// used. Android can't interpret Java 8 byte code.
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile project(':annotation')
    compile 'com.google.auto.service:auto-service:1.0-rc2'
    compile 'com.google.guava:guava:19.0'
    compile 'com.squareup:javapoet:1.0.0'

    // Since this module won't be included in the final apk, it is safe to include it for assisting code generation
    compile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.google.testing.compile:compile-testing:0.9'
}

// Copy test resources files to build directory, so unit tests can find them.
// It is a known issue: https://code.google.com/p/android/issues/detail?id=64887
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}

processTestResources.dependsOn copyTestResources